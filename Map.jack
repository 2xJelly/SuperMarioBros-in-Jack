class Map
{
  field Array posX, posY;
  field Array gap;
  field Block block;
  field Draw draw;
  field int numGap;
  field int numBlock;
  field int screenPos;
  field int idxLeft, idxRight;
  /* Left and right idx of items in the array */
  /* that need to be shown in current Screen position */
  field int gapLeft, gapRight;
  field int starPosX, starPosY;
  field boolean able_Star;

  constructor Map new()
  {
    let able_Star = true;
    let starPosX = 240;
    let starPosY = 160;

    let numGap = 6;
    let numBlock = 135;
    let screenPos = 0;
    let idxLeft = 0;
    let idxRight = 0;
    let gapLeft = 0;
    let gapRight = 0;


    let block = Block.new();
    let gap = block.getGap();
    let posX = block.getPosX();
    let posY = block.getPosY();

    /* Refresh item in the screen */
    do refresh();
    /* Draw the current map */
    do draw();
    /* Draw the platform */
    do drawGround();
    /*Draw the cloud*/
    do drawCloud();

    return this;
  }

  // Use the star
  method void useStar()
  {
    let able_Star = false;
    return;
  }

  // Check if star is able to use
  method boolean ableStar()
  { return able_Star; }

  // Return position x of star
  method int getStarX()
  { return starPosX; }

  // Return position y of star
  method int getStarY()
  { return starPosY; }

  /* Draw the ground/platform */
	method void drawGround()
	{
    var int drawIdx;
    var int gapIdx;
    var int location;
    var int i;

		let i = 0;
    let drawIdx = 0;
    let gapIdx = gapLeft;
    while(drawIdx < 512)
    {
      /* If there is no gap here, draw a rectangle platform */
      if ( ~( (gapIdx < gapRight) & (drawIdx+screenPos = gap[gapIdx]) ) )
      {
        //do drawBlock(drawIdx + screenPos, 224, 32, 32);
        while(i < 2){
        let location = (224/16*512) + ((drawIdx)/16) + i;
        do draw.land(location);
        let i = i + 1;
        }
        let i = 0;
        while(i < 2){
        let location = ((224/16 + 1)*512) + ((drawIdx)/16 + i);
        do draw.land(location);
        let i = i + 1;
        }

        let i = 0;
        let location = 0;

      }
      else { let gapIdx = gapIdx + 1; }

      let drawIdx = drawIdx + 32;
    }

		return;
	}

  /* Erase the ground/platform */
	method void eraseGround()
	{
		do Screen.setColor(false);
		do Screen.drawRectangle(0, 224, 511, 255);
		return;
	}

  /*Draw the Cloud*/
  method void drawCloud()
  {
    var int location;
    var int location1;
    /*X-position mod 31*/
    let location = ((screenPos+80)/16) - ((((screenPos+80)/16)/31)*31);
    let location1 = ((screenPos+96)/16) - ((((screenPos+96)/16)/31)*31);
    do draw.cloudleftup(512 + location);
    do draw.cloudrightup(512 + location1);
    do draw.cloudleftdown(1024 + location);
    do draw.cloudrightdown(1024 + location1);

    do draw.cloudleftup(522 + location);
    do draw.cloudrightup(522 + location1);
    do draw.cloudleftdown(1034 + location);
    do draw.cloudrightdown(1034 + location1);
    return;
  }

  /*erase the cloud*/
  method void eraseCloud()
  {
    var int location;
    var int location1;
    let location = (screenPos+80)/16 - ((((screenPos+80)/16)/31)*31);
    let location1 = (screenPos+96)/16 - ((((screenPos+96)/16)/31)*31);
    do draw.eraseblock(512 + location);
    do draw.eraseblock(512 + location1);
    do draw.eraseblock(1024 + location);
    do draw.eraseblock(1024 + location1);

    do draw.eraseblock(522 + location);
    do draw.eraseblock(522 + location1);
    do draw.eraseblock(1034 + location);
    do draw.eraseblock(1034 + location1);
    return;
  }

  /* Draw the map */
  method void draw()
  {
    var int i;
    var int location;
    var int switch;
    let i = idxLeft;

    do Screen.setColor(true);

    /* Loop throught all items, and draw them */
    while(i < idxRight)
    {
      /* Draw all the blocks */
      if (~(block.getType(i) = 1))
      {
        let switch = block.getType(i);
        if(switch = 0)
        {
          let location = (posY[i]/16*512) + ((posX[i]-screenPos)/16);
          do draw.brick(location);
          let location = 0;
        }
        if(switch = 2)
        {
          let location = (posY[i]/16*512) + ((posX[i]-screenPos)/16);
          do draw.question(location);
          let location = 0;
        }

        if(switch = 4)
        {
          let location = (posY[i]/16*512) + ((posX[i]-screenPos)/16);
          do draw.wall(location);
          let location = 0;
        }

        if(switch = 5)
        {
          let location = (posY[i]/16*512) + ((posX[i]-screenPos)/16);
          do draw.square(location);
          let location = 0;
        }
        if(switch = 3)
        {
          let location = (posY[i]/16*512) + ((posX[i]-screenPos)/16);
          do draw.pipeleft(location);
          let location = 0;
        }

        if(switch = 6)
        {
          let location = (posY[i]/16*512) + ((posX[i]-screenPos)/16);
          do draw.pipetopleft(location);
          let location = 0;
        }

        if(switch = 7)
        {
          let location = (posY[i]/16*512) + ((posX[i]-screenPos)/16);
          do draw.pipetopright(location);
          let location = 0;
        }

        if(switch = 8)
        {
          let location = (posY[i]/16*512) + ((posX[i]-screenPos)/16);
          do draw.piperight(location);
          let location = 0;
        }
      }
      let i = i + 1;
    }

    return;
  }

  /* Erase the map */
  method void erase()
  {
    var int i;
    var int location;
    var int switch;
    let i = idxLeft;

    /* Loop throught all items, and erase them */
    while(i < idxRight)
    {
      /* Erase all the blocks */
      let location = (posY[i]/16*512) + ((posX[i]-screenPos)/16);
      do draw.eraseblock(location);
      let i = i + 1;
    }
    return;
  }

  /* Draw a block */
  /* Assume that we have already setColor as color needed */
  // method void drawGround(int curX, int curY, int sizeX, int sizeY)
  // {
  //   // do Screen.setColor(true);
	// 	do draw.brick(curX - screenPos, curY, curX + (sizeX-1) - screenPos, curY + (sizeY-1) );
	// 	return;
  // }


  /* Move the map to the right by move pixels */
	method void transition(int move)
	{

    do erase();
    do eraseGround();
    do eraseCloud();

    let screenPos = screenPos + move;
    do refresh();

    do draw();
    do drawGround();
    do drawCloud();
    return;
	}

  /* Add / Delete items according to the screen position */
  method void refresh()
  {
    /* For Block */
    /* If the next item is in the screen, added it to the drawing List */
    while ( (idxRight < numBlock) & (posX[idxRight] < (screenPos + 512) ) )
    { let idxRight = idxRight + 1; }

    /* If item in pos[idxLeftO] is no longer in the screen, discard pos[idxLeft] */
    while ( (idxLeft < idxRight) & (posX[idxLeft] < screenPos) )
    { let idxLeft = idxLeft + 1; }

     while ( (gapRight < numGap) & (gap[gapRight] < (screenPos + 512) ) )
    { let gapRight = gapRight + 1; }

    /* For Gap */
    while ( (gapLeft < gapRight) & (gap[gapLeft] < screenPos) )
    { let gapLeft = gapLeft + 1; }

    return;
  }

  /* Check if Mario is stepping on the platform */
  method boolean touchGround(int X, int Y)
  {
    var boolean onG;
    var int l, r, gapIdx;
    let onG = true;
    let gapIdx = gapLeft;

    /* If Mario is not at 208, it is not on the platform */
    if (~(Y = 208)) { let onG = false; }

    /* Check gap no.1 first */
    let l = gap[gapIdx];
    let r = gap[gapIdx] + 32;
    if ((gapIdx < gapRight) & (l < (X-1)) & ((X+15) < r) & (208 = Y)) { let onG = false; }
    let gapIdx = gapIdx + 1;

    while(gapIdx < gapRight)
    {
      if (gap[gapIdx] = r) /* If this gap is connected with the previous gap, connect them */
      { let r = r + 32; }
      else /* Else this is a new gap */
      {
        let l = gap[gapIdx];
        let r = gap[gapIdx] + 32;
      }
      if ((l < (X-1)) & ((X+15) < r) & (208 = Y)) { let onG = false; }
      let gapIdx = gapIdx + 1;
    }

    return onG;
  }

  /* Check if Mario is touching the left side of the gaps */
  method boolean touchLeftSides(int X, int Y)
  {
    var boolean contact;
    var int l, r, gapIdx;
    let contact = false;
    let gapIdx = gapLeft;

    /* Check gap no.1 first */
    let l = gap[gapIdx];
    let r = gap[gapIdx] + 32;
    /* Check if the first gap's left side touches Mario */
    if ((gapIdx < gapRight) & (l = X) & (208 < Y)) { let contact = true; }
    let gapIdx = gapIdx + 1;

    while(gapIdx < gapRight)
    {
      if (gap[gapIdx] = r) /* If this gap is connected with the previous gap, connect them */
      { let r = r + 32; }
      else /* Else this is a new gap */
      {
        let l = gap[gapIdx];
        let r = gap[gapIdx] + 32;
        /* Check if the new gap's left side touches Mario */
        if ((l = X) & (208 < Y)) { let contact = true; }
      }
      let gapIdx = gapIdx + 1;
    }

    return contact;
  }

  /* Check if Mario is touching the right side of the gaps */
  method boolean touchRightSides(int X, int Y)
  {
    var boolean contact;
    var int l, r, gapIdx;
    let contact = false;
    let gapIdx = gapLeft;

    /* Check gap no.1 first */
    let l = gap[gapIdx];
    let r = gap[gapIdx] + 32;
    let gapIdx = gapIdx + 1;

    while(gapIdx < gapRight)
    {
      if (gap[gapIdx] = r) /* If this gap is connected with the previous gap, connect them */
      { let r = r + 32; }
      else /* Else this is a new gap */
      {
        /* Check if the old gap's right side touches Mario */
        if ((r = (X+16)) & (208 < Y)) { let contact = true; }
        let l = gap[gapIdx];
        let r = gap[gapIdx] + 32;
      }
      let gapIdx = gapIdx + 1;
    }

    /* If there is at least 1 gap */
    if (gapLeft < gapRight)
    {
      /* Check if the last gap's right side touches Mario */
      if ((r = (X+16)) & (208 < Y)) { let contact = true; }
    }

    return contact;
  }

  /* Check if Mario is touching any Block from left */
  method boolean touchLeft(int X, int Y)
  {
    var boolean contact, corner;
    var int i;
    var int diffX, diffY;

    let i = idxLeft;
    let contact = false;
    let corner = false;

    while(i < idxRight)
    {
      /* Calculate absolute difference in X and Y */
      let diffX = X - posX[i];
      let diffY = posY[i] - Y;
      if (diffY < 0) { let diffY = -diffY; }

      /* If there are just connected at the corner, it does not count as touching */
      if( (diffX = 16) & (diffY = 16) ) { let corner = true; }

      /* If they are close enough, they are touching */
      if( (~(block.getType(i) = 1)) & ((~corner) & (diffY < 16)) & ( ( (0 = diffX)|(0 < diffX) ) & (diffX < 17)) )
      { let contact = true; }

      let i = i + 1;
      let corner = false;
    }

    return contact;
  }

  /* Check if Mario is touching any Block from right */
  method boolean touchRight(int X, int Y)
  {
    var boolean contact, corner;
    var int i;
    var int diffX, diffY;

    let i = idxLeft;
    let contact = false;
    let corner = false;

    while(i < idxRight)
    {
      /* Calculate absolute difference in X and Y */
      let diffX = posX[i] - X;
      let diffY = posY[i] - Y;
      if (diffY < 0) { let diffY = -diffY; }

      /* If there are just connected at the corner, it does not count as touching */
      if( (diffX = 16) & (diffY = 16) ) { let corner = true; }

      /* If they are close enough, they are touching */
      if( (~(block.getType(i) = 1)) & ((~corner) & (diffY < 16)) & ( ( (0 = diffX)|(0 < diffX) ) & (diffX < 17)) )
      { let contact = true; }

      let i = i + 1;
      let corner = false;
    }

    return contact;
  }

  /* Check if Mario is touching any Block from above */
  method boolean touchAbove(int X, int Y)
  {
    var boolean corner, touch;
    var int i, touchBlock, area;
    var int diffX, diffY;
    var int location;

    let corner = false;
    let touch = false;
    let i = idxLeft;
    let touchBlock = -1;
    let area = 0;

    while(i < idxRight)
    {
      /* Calculate absolute difference in X and Y */
      let diffX = posX[i] - X;
      if (diffX < 0) { let diffX = -diffX; }
      let diffY = Y - posY[i];

      /* If there are just connected at the corner, it does not count as touching */
      if( (diffX = 16) & (diffY = 16) ) { let corner = true; }

      /* If this block is not empty, both block and Mario are close enough,
      this block has more contact area with Mario then the previous one, then they are touching */
      if( (~(block.getType(i) = 1)) & ((~corner) & (diffX < 16)) & ( ( (0 = diffY)|(0 < diffY) ) & (diffY < 17)) & ((16-diffX) > area) )
      {
        let touch = true;
        let touchBlock = i;
        let area = 16-diffX;
      }

      let i = i + 1;
      let corner = false;
    }

    // Debug Code
    /*
    if (touch)
    {
      do Output.moveCursor(1, 0);
      do Output.printString("Block Type:");
			do Output.printInt(block.getType(touchBlock));
      do Output.moveCursor(2, 0);
      do Output.printString("Area:");
			do Output.printInt(area/10);
      do Output.printInt(area - ((area/10)*10) );
    }
    */

    /* If this is a normal block, Mario will crush it into nothing */
    if ( touch & (block.getType(touchBlock) = 0) )
    {
      do block.changeType(touchBlock, 1);
      do erase();
      do draw();
    }

    else{
      if(touch & (block.getType(touchBlock) = 2) )
      {
        do SuperMarioGame.addPoints(100);
        let location = (((posY[touchBlock]/16)-1)*512) + ((posX[touchBlock]-screenPos)/16);
        do erase();
        do block.changeType(touchBlock, 5);
        do draw();
        do draw.coin(location);
        do draw.eraseblock(location);
        let location = location -512;
        do draw.coin(location);
        do Sys.wait(100);
        do draw.eraseblock(location);
      }
    }

    return touch;
  }

  /* Check if Mario is touching any Block from below */
  method boolean touchBelow(int X, int Y)
  {
    var boolean contact, corner;
    var int i;
    var int diffX, diffY;

    let i = idxLeft;
    let contact = false;
    let corner = false;

    while(i < idxRight)
    {
      /* Calculate absolute difference in X and Y */
      let diffX = posX[i] - X;
      if (diffX < 0) { let diffX = -diffX; }
      let diffY = posY[i] - Y;

      /* If there are just connected at the corner, it does not count as touching */
      if( (diffX = 16) & (diffY = 16) ) { let corner = true; }

      /* If they are close enough, they are touching */
      if( (~(block.getType(i) = 1)) & ((~corner) & (diffX < 16)) & ( ( (0 = diffY)|(0 < diffY) ) & (diffY < 17)) )
      { let contact = true; }

      let i = i + 1;
      let corner = false;
    }

    return contact;
  }


}
